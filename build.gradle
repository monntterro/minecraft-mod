plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id 'com.google.protobuf' version '0.9.4'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"template-mod" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation 'com.google.protobuf:protobuf-java:3.25.1'

	implementation 'org.hibernate.orm:hibernate-core:6.4.4.Final'
	implementation 'org.postgresql:postgresql:42.7.3'
	implementation 'com.zaxxer:HikariCP:5.1.0'

	include 'com.google.protobuf:protobuf-java:3.25.1'
	include 'org.hibernate.orm:hibernate-core:6.4.4.Final'
	include 'org.postgresql:postgresql:42.7.3'
	include 'com.zaxxer:HikariCP:5.1.0'
	include 'org.hibernate.common:hibernate-commons-annotations:6.0.6.Final'
	include 'io.smallrye:jandex:3.1.2'
	include 'com.fasterxml:classmate:1.5.1'
	include 'net.bytebuddy:byte-buddy:1.14.11'
	include 'org.jboss.logging:jboss-logging:3.5.0.Final'
	include 'jakarta.persistence:jakarta.persistence-api:3.1.0'
	include 'jakarta.transaction:jakarta.transaction-api:2.0.1'
	include 'org.antlr:antlr4-runtime:4.13.0'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.25.1'
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				java {}
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {
	}
}